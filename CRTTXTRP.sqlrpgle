      //Ç-----------------------------------------------------------------------                    
      //Ç                                                                                           
      //Ç                          Å (C) C.V.G Ä                                                    
      //Ç                                                                                           
      //ÇûPROGRAMAÄ    - CRTTXT            ûFECHAÄ 23/04/2008                                       
      //Ç                                                                                           
      //ÇûAUTORÄ       - Carlos Vega GarcÌa.                                                        
      //Ç                                                                                           
      //ÇûDESCRIPCI”NÄ - Pasar fichero de base de datos a directorio                                
      //Ç                                                                                           
      //Ç-----------------------------------------------------------------------                    
                                                                                                    
      //Ç-----------------------------------------------------------------------                    
      //ÇOpciones de compilaciÛn de especificaciÛn de control.                                      
      //Ç-----------------------------------------------------------------------                    
     H DftActGrp(*No) ActGrp('QC2LE') BndDir('QC2LE')                                               
     H UsrPrf(*User)                                                                                
                                                                                                    
      //Ç-----------------------------------------------------------------------                    
      //ÇDeclaraciÛn de ficheros.                                                                   
      //Ç-----------------------------------------------------------------------                    
     FQAFDPHY   IF   E             Disk    UsrOpn ExtFile('QTEMP/QRCDLEN')                          
                                                                                                    
      //Ç-----------------------------------------------------------------------                    
      //ÇDeclaraciÛn de subprocedimientos (funciones).                                              
      //Ç-----------------------------------------------------------------------                    
      //ÇEjecutar un mandato CL.                                                                    
     D ClCmd           PR                  Like(StatusCode)                                         
     D   Cmd                       2048A   Options(*VarSize) Const                                  
                                                                                                    
      //Ç-----------------------------------------------------------------------                    
      //ÇDeclaraciÛn de procedimientos externos de C para fichero de sistema.                       
      //Ç-----------------------------------------------------------------------                    
     D fopen           PR              *   ExtProc('fopen')                                         
     D   FI_                           *   Value Options(*String)                                   
     D   OpenMode                      *   Value Options(*String)                                   
     Ç*                                                                                             
     D fgets           PR              *   ExtProc('fgets')                                         
     D   Var_                          *   Value Options(*String)                                   
     D   Size_                       10i 0 Value                                                    
     D   FI_                           *   Value                                                    
     Ç*                                                                                             
     D fclose          PR            10i 0 ExtProc('fclose')                                        
     D   FI_                           *   Value                                                    
                                                                                                    
      //Ç-----------------------------------------------------------------------                    
      //ÇDeclaraciÛn de procedimientos externos de C para ficheros STMF.                            
      //Ç-----------------------------------------------------------------------                    
     D fopenO          PR              *   ExtProc( '_C_IFS_fopen')                                 
     D   Stmf_                         *   Value Options(*String)                                   
     D   OpenMode                      *   Value Options(*String)                                   
     Ç*                                                                                             
     D fputcO          PR            10i 0 ExtProc('_C_IFS_fputc')                                  
     D   Int                          5i 0 Value                                                    
     D   FO_                           *   Value                                                    
     Ç*                                                                                             
     D fputsO          PR            10i 0 ExtProc('_C_IFS_fputs')                                  
     D   Var_                          *   Value Options(*String)                                   
     D   FI_                           *   Value                                                    
     Ç*                                                                                             
     D fcloseO         PR            10i 0 ExtProc('_C_IFS_fclose')                                 
     D   FO_                           *   Value                                                    
                                                                                                    
      //Ç-----------------------------------------------------------------------                    
      //ÇDeclaraciÛn de estructuras.                                                                
      //Ç-----------------------------------------------------------------------                    
     D PSDS           SDS                                                                           
     D  Usr                  254    263                                                             
     D  StatusCode       *Status                                                                    
                                                                                                    
      //Ç-----------------------------------------------------------------------                    
      //ÇDeclaraciÛn de variables.                                                                  
      //Ç-----------------------------------------------------------------------                    
      //ÇManejador de fichero de entrada ( Sistema ).                                               
     D FI              S               *                                                            
      //ÇManejador de fichero de salida  ( STMF ).                                                  
     D FO              S               *                                                            
      //ÇValor de retorno tipo entero.                                                              
     D Rc              S              5i 0                                                          
      //ÇVariable auxiliar.                                                                         
     D i               S             10i 0                                                          
     Ç*                                                                                             
     D Ds_Cadena_para_lectura_del_fichero...                                                        
     D                 Ds                                                                           
     D Cad                        32767A                                                            
     D  CadI                          3u 0 Dim(%Size(Cad)) Overlay(Cad)                             
     D  Cadx                          1A   Dim(%Size(Cad)) Overlay(Cad)                             
      //ÇLongitud de registro.                                                                      
     D RcdLen          S                   Like(PHMXRL) Inz                                         
      //ÇFichero de sistema: Libreria/Fichero(Miembro).                                             
     D SFile           S            128A   Varying                                                  
      //ÇFichero continuo (STMF): ruta + nombre de fichero.                                         
     D Stmf            S            512A   Varying                                                  
                                                                                                    
      //Ç-----------------------------------------------------------------------                    
      //ÇDeclaraciÛn de constantes.                                                                 
      //Ç-----------------------------------------------------------------------                    
      //ÇAvance de lÌnea.                                                                           
     D LF              C                   x'25'                                                    
                                                                                                    
      //Ç-----------------------------------------------------------------------                    
      //ÇDeclaraciÛn de par·metros de entrada.                                                      
      //Ç-----------------------------------------------------------------------                    
     C     *Entry        PList                                                                      
     C                   Parm                    PFil             10                                
     C                   Parm                    PLib             10                                
     C                   Parm                    PMbr             10                                
     C                   Parm                    PDir            256                                
     C                   Parm                    PStmf           256                                
                                                                                                    
      /Free                                                                                         
       //Ç----------------------------------------------------------------------                    
       //ÇCuerpo principal                                                                          
       //Ç----------------------------------------------------------------------                    
       If %Parms >= 5;                                                                              
         ExSr ChkObj;                                                                               
       EndIf;                                                                                       
                                                                                                    
       *InLr = *On;                                                                                 
                                                                                                    
       //Ç----------------------------------------------------------------------                    
       //ÇChequear la existencia de los objetos                                                     
       //Ç----------------------------------------------------------------------                    
       BegSr ChkObj;                                                                                
                                                                                                    
         Stmf = %Trim(%Trim(PDir)+'/'+%trim(PStmf));                                                
                                                                                                    
         Rc=Clcmd('CHKOBJ OBJ('+%Trim(PLib)+'/'+%Trim(PFil)+') ' +                                  
                         'OBJTYPE(*FILE) MBR('+%Trim(PMbr)+')');                                    
                                                                                                    
         If Rc = 0;                                                                                 
           ExSr GetRcdLen;                                                                          
         EndIf;                                                                                     
       EndSr;                                                                                       
                                                                                                    
       //Ç----------------------------------------------------------------------                    
       //ÇObtener longitud de registro                                                              
       //Ç----------------------------------------------------------------------                    
       BegSr GetRcdLen;                                                                             
                                                                                                    
         //ÇVolcar informaciÛn del fichero de sistema                                               
         Rc = Clcmd('DSPFD'                                 +                                       
                           ' '+%Trim(PLiB)+'/'+%Trim(PFil)  +                                       
                           ' TYPE(*ATR)'                    +                                       
                           ' OUTPUT(*OUTFILE)'              +                                       
                           ' FILEATR(*PF)'                  +                                       
                           ' OUTFILE(QTEMP/QRCDLEN)'        +                                       
                           ' OUTMBR(*FIRST *REPLACE)');                                             
         //ÇSi no hay errores abre el fichero y contin˙a el proceso.                                
         If Rc = 0;                                                                                 
           Open QAFDPHY;                                                                            
           Read(e) QAFDPHY;                                                                         
           //ÇRecupera la longitud de registro.                                                     
           If Not %Eof(QAFDPHY);                                                                    
             RcdLen = PHMXRL;                                                                       
           EndIf;                                                                                   
           Close QAFDPHY;                                                                           
         EndIf;                                                                                     
         //ÇContinuar si la longitud de registro no es cero.                                        
         If RcdLen <> 0;                                                                            
           //ÇFicheros sin DDS.                                                                     
           If PHFLS = 'N';                                                                          
             ExSr FLAT_To_TXT;                                                                      
           Else;                                                                                    
             //ÇFicheros con DDS.                                                                   
             Select;                                                                                
             //ÇFicheros PF-SRC.                                                                    
             When PHDTAT = 'S';                                                                     
               ExSr SRC_To_TXT;                                                                     
             //ÇFicheros PF-DAT.                                                                    
             When PHDTAT = 'D';                                                                     
               ExSr DAT_To_TXT;                                                                     
             EndSl;                                                                                 
           EndIf;                                                                                   
         EndIf;                                                                                     
                                                                                                    
       EndSr;                                                                                       
                                                                                                    
       //Ç----------------------------------------------------------------------                    
       //ÇCopiar datos de fichero plano de sistema a fichero continuo (STMF).                       
       //Ç----------------------------------------------------------------------                    
       BegSr FLAT_To_TXT;                                                                           
                                                                                                    
         //ÇComponer nombre del fichero de sistema                                                  
         SFile = %Trim(%Trim(Plib)+'/'+%Trim(PFil)+'('+%Trim(PMbr)+')');                            
         //ÇAbre fichero de sistema. Lectura binaria y ccsid 819 (win)                              
         FI = fopen( %Trim(SFile) : 'rb, ccsid=819');                                               
         If FI <> *Null;                                                                            
           //ÇAbrir fichero de salida con p·gina de cÛdigos 819.                                    
           FO  = fopenO( %Trim(Stmf) : 'w, codepage=819');                                          
           Rc  = fcloseO(FO);                                                                       
           FO  = fopenO( %Trim(Stmf) : 'w');                                                        
           If FO <> *Null ;                                                                         
             //ÇLeer FI mientras haya datos                                                         
             Dow fgets( %Addr(Cad) : RcdLen + 1 : FI ) <> *Null;                                    
               rc = fputsO( Cad : FO);  //ÇEscribir datos en FO                                     
               rc = fputcO( LF  : FO);  //ÇEscribir datos en FO                                     
             EndDo;                                                                                 
           EndIf;                                                                                   
         EndIf;                                                                                     
         //ÇCerrar ficheros FO y FI.                                                                
         Rc = fcloseO(FO);                                                                          
         Rc = fclose(FI);                                                                           
         //ÇEstablecer autorizaciones al fichero creado.                                            
         ExSr ChgAut;                                                                               
                                                                                                    
       EndSr;                                                                                       
                                                                                                    
       //Ç----------------------------------------------------------------------                    
       //ÇCopiar datos de fichero SRC de sistema a fichero continuo (STMF).                         
       //Ç----------------------------------------------------------------------                    
       BegSr SRC_To_TXT;                                                                            
                                                                                                    
         //ÇComponer nombre del fichero de sistema                                                  
         SFile = %Trim(%Trim(Plib)+'/'+%Trim(PFil)+' '+%Trim(PMbr));                                
         //ÇCrear fichero continuo con ccsid 819 (UTF)                                              
         FO  = fopenO( %Trim(Stmf) : 'w, codepage=819' );                                           
         //ÇSi lo ha creado, lo cierra y le copia los datos.                                        
         If FO <> *NULL;                                                                            
           fcloseO(FO);                                                                             
           //ÇHacer copia de fichero de importaciÛn.                                                
           Rc = Clcmd('CPYTOIMPF FROMFILE(' + %Trim(SFile) + ')' +                                  
                      ' TOSTMF(''' + %Trim(Stmf) + ''')'         +                                  
                      ' MBROPT(*REPLACE)'                        +                                  
                      ' RCDDLM(*CRLF) DTAFMT(*FIXED)'            +                                  
                      ' STRDLM(*NONE) FLDDLM('''')');                                               
           //ÇEstablecer autoriaciones al fichero creado.                                           
           ExSr ChgAut;                                                                             
         EndIf;                                                                                     
                                                                                                    
       EndSr;                                                                                       
                                                                                                    
       //Ç----------------------------------------------------------------------                    
       //ÇCopiar datos de fichero de sistema a fichero continuo (STMF).                             
       //Ç----------------------------------------------------------------------                    
       BegSr DAT_To_TXT;                                                                            
                                                                                                    
         //ÇComponer nombre del fichero de sistema                                                  
         SFile = %Trim(%Trim(Plib)+'/'+%Trim(PFil)+'('+%Trim(PMbr)+')');                            
         //ÇAbre fichero de sistema.                                                                
         FI  = fopen( %Trim(SFile) : 'rb' );                                                        
         If FI <> *Null;                                                                            
           //ÇAbre fichero continuo para escritura con ccsid 819 (UTF)                              
           FO = fopenO( %Trim(Stmf) : 'w, codepage=819' );                                          
           Rc = fcloseO(FO);                                                                        
           FO = fopenO( %Trim(Stmf) : 'w');                                                         
           If FO <> *Null ;                                                                         
             //ÇLeer FI mientras haya datos.                                                        
             Dow fgets( %Addr(Cad) : RcdLen + 1 : FI ) <> *Null;                                    
               For i=1 to RcdLen;      //ÇEscribir datos en FO.                                     
                 rc=fputcO( CadI(I) : FO);                                                          
               EndFor;                                                                              
               rc = fputcO( LF : FO);  //ÇLF - Avance de lÌnea.                                     
             EndDo;                                                                                 
           EndIf;                                                                                   
           //ÇCerrar ficheros FO y FI.                                                              
           rc = fcloseO(FO);                                                                        
           rc = fclose(FI);                                                                         
           //ÇEstablecer autoriaciones al fichero creado.                                           
           ExSr ChgAut;                                                                             
                                                                                                    
         EndIf;                                                                                     
                                                                                                    
       EndSr;                                                                                       
                                                                                                    
       //Ç----------------------------------------------------------------------                    
       //ÇDar autorizaciÛn PUBLIC(*ALL) y quitar las del usuario.                                   
       //Ç----------------------------------------------------------------------                    
       BegSr ChgAut;                                                                                
                                                                                                    
         If Rc = 0;                                                                                 
           //ÇDar autorizaciÛn PUBLIC(*ALL) al STMF                                                 
           rc =Clcmd('CHGAUT OBJ('''+ %Trim(Stmf) + ''') ' +                                        
                     'USER(*PUBLIC) DTAAUT(*RWX) OBJAUT(*ALL)');                                    
           //ÇQuitar autorizaciÛn USER(USUARIO) al STMF                                             
           rc= Clcmd('CHGAUT OBJ('''+ %Trim(Stmf) + ''') ' +                                        
                     'USER('+ %Trim(Usr) + ') DTAAUT(*NONE) OBJAUT(*NONE)');                        
         EndIf;                                                                                     
                                                                                                    
       EndSr;                                                                                       
      /End-Free                                                                                     
                                                                                                    
       //Ç----------------------------------------------------------------------                    
       //ÇEjecutar un mandato CL.                                                                   
       //Ç----------------------------------------------------------------------                    
     P ClCmd           B                                                                            
     D                 PI                  Like(StatusCode)                                         
     D   Cmd                       2048A   Options(*VarSize) Const                                  
       //ÇProcedimiento externo para ejecutar un mandato CL                                         
     D QcmdExc         PR                  ExtPgm('QCMDEXC')                                        
     D  QCmd                       2048A   Options(*VarSize) Const                                  
     D  QCmdLen                      15P 5 Const                                                    
      /Free                                                                                         
           Callp(e) QCmdExc(%trim(Cmd):%len(%trim(Cmd)));                                           
           Return StatusCode;                                                                       
      /End-Free                                                                                     
     P                 E                                                                            
